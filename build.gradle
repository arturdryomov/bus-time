buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:2.0.0-beta5"
    }
}

apply plugin: "com.android.application"

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        def versionMajor = 1
        def versionMinor = 3
        def versionPatch = 0

        versionName buildVersionName(versionMajor, versionMinor, versionPatch)
        versionCode buildVersionCode(versionMajor, versionMinor, versionPatch)

        minSdkVersion 9
        targetSdkVersion 23

        resConfigs "en", "ru"
    }

    signingConfigs {
        release {
            def signingFile = file("signing.properties")
            def signingProperties = new Properties()

            if (signingFile.exists()) {
                signingProperties.load(signingFile.newInputStream())

                storeFile file(signingProperties["keystore.file"])
                storePassword signingProperties["keystore.password"]
                keyAlias signingProperties["key.alias"]
                keyPassword signingProperties["key.password"]
            }
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }

    productFlavors {
        play {
            applicationId "ru.ming13.bustime"
        }

        beta {
            applicationId "ru.ming13.bustime.beta"
        }
    }

    applicationVariants.all { variant ->
        variant.resValue "string", "application_id", variant.applicationId
    }

    packagingOptions {
        exclude "META-INF/LICENSE.txt"
        exclude "META-INF/NOTICE.txt"

        exclude "META-INF/services/javax.annotation.processing.Processor"
    }
}

def buildVersionName(versionMajor, versionMinor, versionPatch) {
    return "${versionMajor}.${versionMinor}.${versionPatch}"
}

def buildVersionCode(versionMajor, versionMinor, versionPatch) {
    return versionMajor * 10 ** 2 + versionMinor * 10 ** 1 + versionPatch * 10 ** 0
}

dependencies {
    repositories {
        jcenter()
    }

    compile "com.android.support:appcompat-v7:21.0.3"
    compile "com.android.support:support-annotations:21.0.3"
    compile "com.android.support:support-v4:21.0.3"
    compile "com.github.frankiesardo:icepick:2.3.6"
    compile "com.google.android.gms:play-services-location:6.5.87"
    compile "com.google.android.gms:play-services-maps:6.5.87"
    compile "com.google.maps.android:android-maps-utils:0.3.4"
    compile "com.jakewharton:butterknife:6.0.0"
    compile "com.f2prateek.dart:dart:1.1.0"
    compile "com.nispok:snackbar:2.7.5"
    compile "com.squareup.okio:okio:1.6.0"
    compile "com.squareup:otto:1.3.8"
    compile "com.squareup.retrofit:retrofit:1.9.0"
    compile "com.venmo.cursor:library:0.4"
    compile "info.metadude.android:typed-preferences:2.1.0"
    compile "org.ocpsoft.prettytime:prettytime:4.0.1.Final"

    provided "com.github.frankiesardo:icepick-processor:2.3.6"
}
