buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:1.0.+"
        classpath "com.jakewharton.sdkmanager:gradle-plugin:0.12.+"
    }
}

apply plugin: "android-sdk-manager"
apply plugin: "com.android.application"

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        def versionMajor = 1
        def versionMinor = 2
        def versionPatch = 0

        versionName buildVersionName(versionMajor, versionMinor, versionPatch)
        versionCode buildVersionCode(versionMajor, versionMinor, versionPatch)

        minSdkVersion 9
        targetSdkVersion 21

        resConfigs "nodpi", "mdpi", "hdpi", "xhdpi", "xxhdpi"
        resConfigs "en", "ru"
    }

    signingConfigs {
        release {
            def signingFile = file("signing.properties")
            def signingProperties = new Properties()

            if (signingFile.exists()) {
                signingProperties.load(signingFile.newInputStream())

                storeFile file(signingProperties["keystore.file"])
                storePassword signingProperties["keystore.password"]
                keyAlias signingProperties["key.alias"]
                keyPassword signingProperties["key.password"]
            }
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }

    productFlavors {
        play {
            applicationId "ru.ming13.bustime"

            resValue "string", "application_id", applicationId
        }

        beta {
            applicationId "ru.ming13.bustime.beta"

            resValue "string", "application_id", applicationId
        }
    }

    packagingOptions {
        exclude "META-INF/LICENSE.txt"
        exclude "META-INF/NOTICE.txt"
    }
}

def buildVersionName(versionMajor, versionMinor, versionPatch) {
    return "${versionMajor}.${versionMinor}.${versionPatch}"
}

def buildVersionCode(versionMajor, versionMinor, versionPatch) {
    return versionMajor * 10 ** 2 + versionMinor * 10 ** 1 + versionPatch * 10 ** 0
}

dependencies {
    repositories {
        mavenCentral()
    }

    compile "com.android.support:appcompat-v7:21.0.+"
    compile "com.android.support:support-annotations:21.0.+"
    compile "com.android.support:support-v4:21.0.+"
    compile "com.github.frankiesardo:icepick:2.3.+"
    compile "com.google.android.gms:play-services-location:6.5.+"
    compile "com.google.android.gms:play-services-maps:6.5.+"
    compile "com.google.maps.android:android-maps-utils:0.3.+"
    compile "com.jakewharton:butterknife:6.0.+"
    compile "com.nispok:snackbar:2.7.+"
    compile "com.squareup:otto:1.3.+"
    compile "com.squareup.retrofit:retrofit:1.8.+"
    compile "com.venmo.cursor:library:0.3"
    compile "commons-io:commons-io:2.4"
    compile "org.apache.commons:commons-lang3:3.3.+"
    compile "org.ocpsoft.prettytime:prettytime:3.2.5.Final"

    provided "com.github.frankiesardo:icepick-processor:2.3.+"
}

task generateWrapper(type: Wrapper) {
    description "Generates Gradle wrapper."

    gradleVersion = "2.2.1"
}
